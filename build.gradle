plugins {
    // Basic Plugins
    id 'java-library'
    id 'groovy'
    id 'jacoco'

    id 'nebula.project' version '3.4.0'

    // Dependency Management Support Plugins
    id 'com.github.ben-manes.versions' version '0.15.0'

    // Versioning Plugins
    id 'nebula.release' version '6.0.2'

    // Quality / Documentation Plugins
    id 'org.sonarqube' version '2.5'
    id 'org.asciidoctor.convert' version '1.5.3'
}

apply from: 'gradle/asciidoctor.gradle'
apply from: 'gradle/ben-manes.versions.gradle'

group = 'io.eliez.slog4j'

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
    jcenter()
}

dependencies {
    compileOnly(
            [group: 'org.projectlombok', name: 'lombok', version: '1.16.18'],
    )

    api(
            [group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'],
    )

    implementation(
            [group: 'org.apache.commons', name: 'commons-text', version: '1.1'],
    )

    testCompile(
            [group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.4.12'],
            [group: 'org.spockframework', name: 'spock-core', version: '1.1-groovy-2.4'],
    )
}

tasks.withType(Test) {
    systemProperties = System.properties as Map

    // show standard out and standard error of the test JVM(s) on the console
    testLogging {
        events 'started', 'passed', 'skipped', 'failed'
        showStandardStreams = true
    }
}

publishing {
    repositories {
        maven {
            url rctiReleasesRepo
            credentials {
                username "${project.properties.deployerUsername}"
                password "${project.properties.deployerPassword}"
            }
        }
    }
}
