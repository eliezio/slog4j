plugins {
    // Basic Plugins
    id 'java'
    id 'groovy'
    id 'jacoco'

    id 'nebula.project' version '3.4.0'
    id 'io.spring.dependency-management' version '1.0.3.RELEASE'

    // Versioning Plugins
    id 'nebula.release' version '6.0.1'

    // Quality / Documentation Plugins
    id 'org.sonarqube' version '2.5'
    id 'org.asciidoctor.convert' version '1.5.3'
}

apply from: 'gradle/asciidoctor.gradle'

group = 'io.eliez.slog4j'

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
    jcenter()
}

dependencyManagement {
    imports {
        mavenBom 'io.spring.platform:platform-bom:Brussels-SR4'
    }
}

dependencies {
    compileOnly(
            [group: 'org.projectlombok', name: 'lombok', version: '1.16.18'],
    )

    compile(
            [group: 'org.apache.commons', name: 'commons-text', version: '1.1'],
            [group: 'org.slf4j', name: 'slf4j-api'],
    )

    testCompile(
            [group: 'org.projectlombok', name: 'lombok'],
            [group: 'org.codehaus.groovy', name: 'groovy-all'],
            [group: 'org.spockframework', name: 'spock-core'],
    )
}

tasks.withType(Test) {
    systemProperties = System.properties as Map

    // show standard out and standard error of the test JVM(s) on the console
    testLogging {
        events 'started', 'passed', 'skipped', 'failed'
        showStandardStreams = true
    }
}

publishing {
    repositories {
        maven {
            url rctiReleasesRepo
            credentials {
                username "${project.properties.deployerUsername}"
                password "${project.properties.deployerPassword}"
            }
        }
    }
}
